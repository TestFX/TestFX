/*
 * Copyright 2013-2014 SmartBear Software
 * 
 * Licensed under the EUPL, Version 1.1 or - as soon they will be approved by the European
 * Commission - subsequent versions of the EUPL (the "Licence"); You may not use this work
 * except in compliance with the Licence.
 *
 * You may obtain a copy of the Licence at:
 * http://ec.europa.eu/idabc/eupl
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * Licence is distributed on an "AS IS" basis, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied. See the Licence for the specific language governing permissions
 * and limitations under the Licence.
 */

apply plugin: 'maven-publish'

def pomConfig = {
    name project.name
    description project.pomDescription
    url 'https://github.com/SmartBear/TestFX'
    inceptionYear '2013'
    licenses {
        license([:]) { // force map arg to overcome license{} from license plugin
            name 'European Union Public Licence - EUPL v.1.1'
            url 'http://ec.europa.eu/idabc/eupl.html'
            distribution 'repo'
        }
    }
    scm {
        url 'https://github.com/SmartBear/TestFX.git'
    }
    developers {
        developer {
            id 'minisu'
            name 'Henrik Olsson'
            url 'https://github.com/minisu'
            timezone '+1'
            roles {
                role 'Developer'
            }
        }
        developer {
            id 'dainnilsson'
            name 'Dain Nilsson'
            url 'https://github.com/dainnilsson'
            timezone '+1'
            roles {
                role 'project founder'
                role 'former developer'
            }
        }
    }
    contributors {
        [
            'Philipp Keck': 'https://github.com/Philipp91',
            'The LoadUI team': 'https://github.com/SmartBear/loadui',
            'Andres Almiray': 'https://github.com/aalmiray'
        ].each { cName, cUrl ->
            contributor {
                name cName
                url cUrl
                roles {
                    role 'Contributor'
                }
            }
        }
    }
}

publishing {
    repositories {
        maven {
            url sonatypeUrl
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

jar {
    manifest {
        attributes(
            'Built-By': System.properties['user.name'],
            'Created-By': System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
            'Build-Date': project.buildDate,
            'Build-Time': project.buildTime,
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Specification-Vendor': project.project_vendor,
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': project.project_vendor
        )
    }
}
